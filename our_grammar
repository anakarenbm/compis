program ::= 'PROGRAM' 'ID' program1
program1 ::= script | html | empty

script ::= 'SCRIPT' block

block ::= 'OBRACE' block1 'CBRACE'
block1 ::= statement block1 | empty

declaration ::= type 'ID'
type ::= 'STR' | 'INT' | 'DOUBLE' | 'CHAR' | 'BOOL'

statement ::= declaration | assignment | condition | cycle | call | module | writing

assignment ::= type assignment1 | assignment1
assignment1 ::= 'ID' 'EQ' expression

expression ::= exp expression1
expression1 ::= 'REL' exp | 'ASSOCIATIVE' exp | empty

exp ::= term exp1
exp1 ::= 'SIGN' exp exp1 | empty

term ::= factor term1
term1 ::= 'OP' term term1 | empty

factor ::= 'OPAREN' expression 'CPAREN' | factor1
factor1 ::= value | 'SIGN' value

value ::= 'ID' | 'CTEI' | 'CTED' | 'CTESTR' | 'FALSE' | 'TRUE'

condition ::= 'IF' 'OPAREN' expression 'CPAREN' block condition1
condition1 ::= 'ELSE' 'OPAREN' expression 'CPAREN' block | empty

cycle ::= 'LOOP' 'OPAREN' expression 'CPAREN' block

module ::= 'DEF' 'ID' 'OPAREN' arguments 'CPAREN' 'COLON' spitval block

call ::= 'ID' call1 | predef call1
call1 ::= 'OPAREN' expression 'CPAREN'

predef ::= 'SUCK_CSV' | 'SORT_SLICE' | 'SORT_2D_SLICE' | 'MEDIAN' | 'MODE' | 'AVG' | 'POW'

writing ::= 'EVAL' 'OPAREN' expression 'CPAREN'

/* falta agregar slices */
arguments ::= type 'ID' arguments1 | empty
arguments1 ::= 'COMMA' type 'ID' arguments1 | empty

spitval ::= type | empty

html ::= htmltag | empty

htmltag ::= tag 'OBRACE' class htmltag1 'CBRACE'
htmltag1 ::= text htmltag1 | htmlscript htmltag1 | htmltag htmltag1 | empty

tag ::= 'H1' | 'H2' | 'DIV' | 'P' | 'TABLE' | 'TR' | 'TH'

text ::= 'CTESTR' | empty

class ::= 'CLASS' 'COLON' 'CTESTR' | empty

htmlscript ::= 'OEVALSCRIPT' expression 'CEVALSCRIPT' | embedscript

embedscript ::= 'EMBED' 'ID' 'OBRACE' eblock 'CBRACE'
